
INCLUDE_DIRECTORIES("${CMAKE_BINARY_DIR}/fem/src")
INCLUDE_DIRECTORIES("${CMAKE_SOURCE_DIR}/hutiter/include")


MACRO(ADD_ELMER_MODULE BASENAME)
  CMAKE_PARSE_ARGUMENTS(_parsedArgs "" "" "SOURCES;DIRECTORY" "${ARGN}")

  SET(_SOURCES "")

  IF(_parsedArgs_DIRECTORY)
    FILE(GLOB DIR_SRC_FILES "${_parsedArgs_DIRECTORY}/*.F90")
    SET(_SOURCES ${DIR_SRC_FILES})
  ELSEIF(_parsedArgs_SOURCES)
    SET(_SOURCES ${_parsedArgs_SOURCES})
  ELSE()
    SET(_SOURCES ${BASENAME}.F90)
  ENDIF()

  ADD_LIBRARY(${BASENAME} SHARED ${_SOURCES})

  SET_TARGET_PROPERTIES(${BASENAME} PROPERTIES PREFIX "")
  SET_TARGET_PROPERTIES(${BASENAME} PROPERTIES LINKER_LANGUAGE Fortran)
  TARGET_LINK_LIBRARIES(${BASENAME} elmersolver)
  ADD_DEPENDENCIES(${BASENAME} elmersolver)
  INSTALL(TARGETS ${BASENAME} 
    LIBRARY DESTINATION "share/elmersolver/lib"
    RUNTIME DESTINATION "share/elmersolver/lib")
    #ARCHIVE DESTINATION "share/elmersolver/lib")
ENDMACRO()

# Turn .F90 files into modules
FILE(GLOB SRC_FILES *.F90)

FOREACH(FNAME ${SRC_FILES})
  GET_FILENAME_COMPONENT(BASENAME ${FNAME} NAME_WE)
  ADD_ELMER_MODULE(${BASENAME})
ENDFOREACH()

# Turn module subdirs into modules
FILE(GLOB SRC_DIRS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} *)

FOREACH(DIRNAME ${SRC_DIRS})
  IF(IS_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/${DIRNAME}")
    ADD_ELMER_MODULE(${DIRNAME} DIRECTORY ${DIRNAME}) 
  ENDIF()
ENDFOREACH()

ADD_DEPENDENCIES(EliminatePeriodic EliminateDirichlet)
ADD_DEPENDENCIES(FourierLoss MagnetoDynamics)
TARGET_LINK_LIBRARIES(EliminatePeriodic EliminateDirichlet)
